package(default_visibility=["//visibility:public"])

load("//:packages.bzl", "CDK_TARGETS", "CDK_PACKAGES", "CDK_TARGETS_WITH_STYLES", "ROLLUP_GLOBALS")
load("//tools:defaults.bzl", "ng_module", "ng_package")

# Root "@angular/cdk" entry-point that does not re-export individual entry-points.
ng_module(
  name = "cdk",
  srcs = glob(["*.ts"], exclude=["**/*.spec.ts"]),
  module_name = "@angular/cdk",
  deps = [
    "@npm//@angular/core",
  ],
)

# Array of style files that need to be copied to the root of the CDK package. This is
# necessary in order to keep the Bazel NPM package similar to the Gulp output.
rootStyles = [file for target in CDK_TARGETS_WITH_STYLES for file in [
  ["_%s.scss" % target, target],
  ["%s-prebuilt.css" % target, target],
  ["%s-prebuilt.css.map" % target, target]
]]

# Copy all stylesheets to the CDK root package. We need to copy these files, as
# these stylesheets need to be at the root of the NPM package (e.g. "_a11y.scss")
[genrule(
  name = "%s_copied" % file,
  srcs = ["//src/cdk/%s:%s" % (target, file)],
  outs = [file],
  cmd = "cp $< $@") for [file, target] in rootStyles]

filegroup(
  name = "overviews",
  srcs = ["//src/cdk/%s:overview" % name for name in CDK_PACKAGES]
)

# Creates the @angular/cdk package published to npm.
ng_package(
  name = "npm_package",
  srcs = ["package.json"],
  entry_point = "src/cdk/public_api.js",
  globals = ROLLUP_GLOBALS,
  deps = CDK_TARGETS,
  data = ["%s_copied" % file for [file, _] in rootStyles],
  tags = ["build-package"],
)
