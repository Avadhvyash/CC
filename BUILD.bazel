package(default_visibility = ["//visibility:public"])

# TODO(jelbourn): figure out if these workarounds are still needed

# TODO: Replace with fine-grained node_modules using `npm_install` / `yarn_install`.
# TODO: See https://github.com/bazelbuild/rules_nodejs/wiki#migrating-to-rules_nodejs-013
filegroup(
  name = "node_modules",
  srcs = glob(["/".join(["node_modules", pkg, "**", ext]) for pkg in [
    "@angular",
    "@angular-devkit",
    "@schematics",
    "@types",
    "bytebuffer",
    "chalk",
    "fs-extra",
    "glob",
    "hammerjs",
    "jasmine",
    "minimist",
    "moment",
    "mock-fs",
    "parse5",
    "protobufjs",
    "protractor",
    "reflect-metadata",
    "rxjs",
    "tsickle",
    "tslib",
    "tslint",
    "tsutils",
    "typescript",
    "zone.js",
  ] for ext in [
    "*.js",
    "*.json",
    "*.d.ts",
  ]] + [
    "node_modules/http-server/**",
    # Reference all files of the "@schematics/angular" package because the schematic
    # tests depend on the template files of the angular schematics.
    "node_modules/@schematics/angular/**",
  ]),
)

# Glob pattern that matches all Angular testing bundles.
ANGULAR_TESTING = [
  "node_modules/@angular/*/bundles/*-testing.umd.js",
  # The compiler and the dynamic platform-browser should be visible only in tests
  "node_modules/@angular/compiler/bundles/*.umd.js",
  "node_modules/@angular/platform-browser-dynamic/bundles/*.umd.js",
]

filegroup(
  name = "angular_bundles",
  srcs = glob(["node_modules/@angular/*/bundles/*.umd.js"], exclude = ANGULAR_TESTING),
)

filegroup(
  name = "angular_test_bundles",
  testonly = 1,
  srcs = glob(ANGULAR_TESTING),
)

filegroup(
  name = "tslib_bundle",
  testonly = 1,
  srcs = glob(["node_modules/tslib/tslib.js"]),
)

# Files necessary for unit tests that use zonejs
filegroup(
  name = "web_test_bootstrap_scripts",
  # The order of these deps is important.
  # Do not sort.
  srcs = [
    "//:node_modules/reflect-metadata/Reflect.js",
    "//:node_modules/zone.js/dist/zone.js",
    "//:node_modules/zone.js/dist/zone-testing.js",
    "//:node_modules/zone.js/dist/task-tracking.js",
  ],
)
